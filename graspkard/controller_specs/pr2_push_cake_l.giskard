scope = {
	unitX = vec3(1,0,0);
	unitY = vec3(0,1,0);
	unitZ = vec3(0,0,1);

	j_torso_lift    = inputJoint("torso_lift_joint");

	j_shoulder_pan  = inputJoint("l_shoulder_pan_joint");
	j_shoulder_lift = inputJoint("l_shoulder_lift_joint");
	j_upper_arm_roll= inputJoint("l_upper_arm_roll_joint");
	j_elbow_flex    = inputJoint("l_elbow_flex_joint");
	j_forearm_roll  = inputJoint("l_forearm_roll_joint");
	j_wrist_flex    = inputJoint("l_wrist_flex_joint");
	j_wrist_roll    = inputJoint("l_wrist_roll_joint");
	j_gripper       = inputJoint("l_gripper_joint");

	cake = inputFrame("cake_frame");
	cake_pos = cake.pos;
	cake_rot = cake.rot;

	torso_lift    = frame(rotation(unitX, 0), vec3(-0.05, 0, 0.739675 + j_torso_lift));
	shoulder_pan  = frame(rotation(unitZ, j_shoulder_pan),   vec3(0, 0.188, 0));
	shoulder_lift = frame(rotation(unitY, j_shoulder_lift),  vec3(0.1, 0, 0));
	upper_arm_roll= frame(rotation(unitX, j_upper_arm_roll), vec3(0, 0, 0));
	elbow_flex    = frame(rotation(unitY, j_elbow_flex),     vec3(0.4, 0, 0));
	forearm_roll  = frame(rotation(unitX, j_forearm_roll),   vec3(0, 0, 0));
	wrist_flex    = frame(rotation(unitY, j_wrist_flex),     vec3(0.321, 0, 0));
	wrist_roll    = frame(rotation(unitX, j_wrist_roll),     vec3(0, 0, 0));

	elbow = torso_lift * shoulder_pan * shoulder_lift * upper_arm_roll * elbow_flex ;
	wrist = elbow * forearm_roll * wrist_flex * wrist_roll;
	knife = wrist * inputFrame("knife_in_wrist");

	# 
	cake_dist = abs((knife.pos - cake_pos) * (cake_rot * unitX));

	knife_rot = knife.rot;

	blade_x = knife_rot * unitX;
	blade_z = knife_rot * unitZ;

	blade_align_x = abs(blade_x * (cake_rot * unitY));
	blade_align_x_err = 1 - blade_align_x;

	blade_roll = blade_z * (cake_rot * unitZ);
	blade_roll_err = 1 - blade_roll ;

	# Limits, weights and the like
	negVelLimitTorso = -0.02;
	posVelLimitTorso =  0.02;
	negVelLimitArm   = -0.6;
	posVelLimitArm   =  0.6;

	weightGripper = 0.001;
	weightArm   = weightGripper * 10;
	weightTorso = weightArm * 10;

	weightPositionGoal = 1;
	weightRotationGoal = 4;

	feedback = 1
}

controllableConstraints = {
	controllableConstraint(negVelLimitTorso, posVelLimitTorso, weightTorso, "torso_lift_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_shoulder_pan_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_shoulder_lift_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_upper_arm_roll_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_elbow_flex_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_forearm_roll_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_wrist_flex_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_wrist_roll_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "l_gripper_joint")
}

softConstraints = {
	softConstraint(0.02, 0.02, weightPositionGoal, cake_dist, "Knife ZY-Dist");
	softConstraint(blade_align_x_err, blade_align_x_err, weightRotationGoal, blade_align_x, "Knife align");
	softConstraint(blade_roll_err, blade_roll_err, weightRotationGoal, blade_roll, "Knife roll")
}	

hardConstraints = {
	hardConstraint(0.0115 - j_torso_lift, 0.325 - j_torso_lift, j_torso_lift);
	
	# # Right arm constraints
	hardConstraint(-2.1353 - j_shoulder_pan,  0.5646 - j_shoulder_pan,   j_shoulder_pan);
	hardConstraint(-0.3536 - j_shoulder_lift, 1.2963 - j_shoulder_lift,  j_shoulder_lift);
	hardConstraint(-3.75   - j_upper_arm_roll,  0.65 - j_upper_arm_roll, j_upper_arm_roll);
	hardConstraint(-2.1213 - j_elbow_flex,     -0.15 - j_elbow_flex,     j_elbow_flex);
	hardConstraint(-2.0    - j_wrist_flex,      -0.1 - j_wrist_flex,     j_wrist_flex)
}