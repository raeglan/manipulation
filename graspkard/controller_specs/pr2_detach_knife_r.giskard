scope = {
	unitX = vec3(1,0,0);
	unitY = vec3(0,1,0);
	unitZ = vec3(0,0,1);

		# Joint input

	j_shoulder_pan  = inputJoint("r_shoulder_pan_joint");
	j_shoulder_lift = inputJoint("r_shoulder_lift_joint");
	j_upper_arm_roll= inputJoint("r_upper_arm_roll_joint");
	j_elbow_flex    = inputJoint("r_elbow_flex_joint");
	j_forearm_roll  = inputJoint("r_forearm_roll_joint");
	j_wrist_flex    = inputJoint("r_wrist_flex_joint");
	j_wrist_roll    = inputJoint("r_wrist_roll_joint");
	j_gripper       = inputJoint("r_gripper_joint");
	j_torso_lift    = inputJoint("torso_lift_joint");

	rack_frame = inputFrame("rack_frame");
	rack_pos = rack_frame.pos;
	rack_rot = rack_frame.rot;

	torso_lift       = frame(rotation(unitX, 0), vec3(-0.05, 0, 0.739675 + j_torso_lift));
	shoulder_pan  = frame(rotation(unitZ, j_shoulder_pan),   vec3(0, -0.188, 0));
	shoulder_lift = frame(rotation(unitY, j_shoulder_lift),  vec3(0.1, 0, 0));
	upper_arm_roll= frame(rotation(unitX, j_upper_arm_roll), vec3(0, 0, 0));
	elbow_flex    = frame(rotation(unitY, j_elbow_flex),     vec3(0.4, 0, 0));
	forearm_roll  = frame(rotation(unitX, j_forearm_roll),   vec3(0, 0, 0));
	wrist_flex    = frame(rotation(unitY, j_wrist_flex),     vec3(0.321, 0, 0));
	wrist_roll    = frame(rotation(unitX, j_wrist_roll),     vec3(0, 0, 0));

	elbow = torso_lift * shoulder_pan * shoulder_lift * upper_arm_roll * elbow_flex ;
	wrist = elbow * forearm_roll * wrist_flex * wrist_roll;
	knife = wrist * inputFrame("knife_frame_wrist");

	# 
	rack_normal = rack_rot * unitY;
	rack_dist = (knife.pos.z - rack_pos.z);

	blade_rack_ang = knife.rot * unitY;

	blade_align = abs(blade_rack_ang * rack_normal);
	blade_align_err = 1 - blade_align;

	# Limits, weights and the like
	negVelLimitTorso = -0.02;
	posVelLimitTorso =  0.02;
	negVelLimitArm   = -0.6;
	posVelLimitArm   =  0.6;

	weightGripper = 0.001;
	weightArm   = weightGripper * 10;
	weightTorso = weightArm * 10;

	weightPositionGoal = 1;
	weightRotationGoal = 4;

	feedback = max(0.1 - abs(rack_dist), 0);

	VIS__feedback = feedback;
	VIS__rack_frame = rack_frame;
	VIS__knife_frame = knife
}

controllableConstraints = {
#	controllableConstraint(negVelLimitTorso, posVelLimitTorso, weightTorso, "torso_lift_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_shoulder_pan_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_shoulder_lift_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_upper_arm_roll_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_elbow_flex_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_forearm_roll_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_wrist_flex_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_wrist_roll_joint");
	controllableConstraint(negVelLimitArm, posVelLimitArm, weightArm, "r_gripper_joint")
}

softConstraints = {
	softConstraint(0.1, 0.1, weightPositionGoal, rack_dist, "Knife Z-Dist");
	softConstraint(blade_align_err, blade_align_err, weightRotationGoal, blade_align, "Knife align")
}	

hardConstraints = {
	hardConstraint(0.0115 - j_torso_lift, 0.325 - j_torso_lift, j_torso_lift);
	
	# # Right arm constraints
	hardConstraint(-2.1353 - j_shoulder_pan,  0.5646 - j_shoulder_pan,   j_shoulder_pan);
	hardConstraint(-0.3536 - j_shoulder_lift, 1.2963 - j_shoulder_lift,  j_shoulder_lift);
	hardConstraint(-3.75   - j_upper_arm_roll,  0.65 - j_upper_arm_roll, j_upper_arm_roll);
	hardConstraint(-2.1213 - j_elbow_flex,     -0.15 - j_elbow_flex,     j_elbow_flex);
	hardConstraint(-2.0    - j_wrist_flex,      -0.1 - j_wrist_flex,     j_wrist_flex)
}